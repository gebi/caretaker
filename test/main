#!/usr/bin/env zsh

# These parameters might disturb caretaker/bootstrap
unset -m 'GIT_*' 'XDG_*'

# Test stuff
setopt err_exit
trap "print -P '\n%N:%i: %B%F{red}Test faild!%F{default}%b'" ZERR

alias ct='ct --quiet --no-colours'

for file in test/lib/*; {
	source $file
}

# This should be run in the real repo
ct_test_documentation

function stringcmp {
	diff -u <(echo $1) <(echo $2)
}

function run_single_test {
	ct_new_test_env
	ct_test_$1
}

ct_repo=$1
export PATH=$PWD/bin:$PATH

run_single_test checklinks
run_single_test bootstrap
run_single_test misc
run_single_test make

for conf_origin in 0 1; {
	for repo in ra ra-bare; {

		ct_new_test_env
		echo "GIT_USE_ORIGIN=$conf_origin" >> $test_home/.config/caretaker/caretaker.conf
		echo "## GIT_USE_ORIGIN=$conf_origin"

		file=${repo%-*}
		if [[ $repo == *-bare ]] {
			echo "## bare repository"
			complement=$file
		} else {
			echo "## non-bare repository"
			complement=${repo}-bare
		}

		ct_test_add $repo
		ct_test_prereqs $file
		cd $test_pdir/$repo
		ct_test_push $repo
		cd $test_pdir
		ct_test_pull_prepare $repo $file
		ct_test_pull $repo $file
		ct_test_remove $repo
	}
}

rm -rf $test_pdir $test_proot $test_home

print -P '%F{green} test passed%F{default}'
