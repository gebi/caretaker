#!/usr/bin/env zsh
unset -m 'GIT_*' 'XDG_*'
setopt err_exit

trap "print -P '\n%N:%i: %B%F{red}Test faild!%F{default}%b'" ZERR
alias ct='ct --quiet --no-colours'

for file in test/lib/*; {
	source $file
}

ct_test_documentation

function stringcmp {
	diff -u <(echo $1) <(echo $2)
}

tests=$PWD/test
test_pdir=$(mktemp -dt pkgdir.XXXXXX)
test_proot=$(mktemp -dt pkgroot.XXXXXX)
test_home=$(mktemp -dt pkghome.XXXXXX)
ct_repo=$1

export PATH=$PWD/bin:$PATH

cat << meow
test directories:
	PKG_DIR = $test_pdir
	PKG_ROOT = $test_proot
	HOME = $test_home
meow

cd $test_home
export HOME=$test_home
ct_test_checklinks
ct_setup_root 1 $test_proot

echo "# bootstrapping PKG_DIR"
$test_proot/caretaker/include/bootstrap $test_proot $test_pdir

echo "# checking for success"
[[ -e $test_home/.config/caretaker/caretaker.conf ]]
[[ -d $test_proot/caretaker ]]
[[ -d $test_pdir/caretaker ]]
[[ -L $test_home/bin/ct ]]
[[ -L $test_home/bin/checklinks ]]
[[ -x $(readlink $test_home/bin/ct) ]]
[[ -x $(readlink $test_home/bin/checklinks) ]]
[[ -e $test_pdir/.collected/man/man1/ct.1 ]]
[[ -e $test_pdir/.collected/man/man5/caretaker.conf.5 ]]
[[ -e $test_pdir/.collected/man/man7/caretaker.7 ]]

echo "# ct list local"
stringcmp "caretaker" "$(ct list)"
stringcmp "caretaker" "$(ct list local)"

echo "# ct list not-installed"
stringcmp "ra\nra-bare\nrb\nrb-bare\nrc\nrc-bare" "$(ct list not-installed)"

echo "# ct list all"
stringcmp "caretaker\nra\nra-bare\nrb\nrb-bare\nrc\nrc-bare" "$(ct list all)"

echo "# ct add (no such repo)"
! ct add weltfrieden

echo "# ct add (already installed)"
! ct add caretaker

for conf_origin in 0 1; {
	echo "## GIT_USE_ORIGIN=$conf_origin"
	echo "GIT_USE_ORIGIN=$conf_origin" >> $test_home/.config/caretaker/caretaker.conf
	for repo in ra ra-bare; {
		file=${repo%-*}
		if [[ $repo == *-bare ]] {
			echo "## bare repository"
			complement=$file
		} else {
			echo "## non-bare repository"
			complement=${repo}-bare
		}
		ct_test_add $repo
		ct_test_prereqs $file
		cd $test_pdir/$repo
		ct_test_push $repo
		cd $test_pdir
		ct_test_pull_prepare $repo $file
		ct_test_remove $repo
	}
}

rm -rf $test_pdir $test_proot $test_home

print -P '%F{green} test passed%F{default}'
